// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    
    //If instruction[15] is 0, A type instr and we don't want any jump bits or ALU outputs to be true so output false
    Mux16(a=false, b=instruction, sel=instruction[15],
    out[0]=j3, out[1]=j2, out[2]=j1,
    out[3]=d3, out[3]=writeM, out[4]=d2, out[5]=d1,
    out[6]=c6, out[7]=c5, out[8]=c4, out[9]=c3, out[10]=c2, out[11]=c1,
    out[12]=a,
    out[15]=insType);

    // If C instr, use ALU output, if A then use the instruction input
    Mux16(a=instruction, b=ALUOut, sel=insType, out=insMuxALU);

    // If C instruction and a (instruction[12]) is 0, A reg, else M reg
    Mux16(a=ARegOut, b=inM, sel=a, out=AmuxM);

    // Load A if instruction type is A or if C instruction destination includes A (6th bit true)
    Not(in=insType, out=AType);
    Or(a=AType, b=d1, out=loadA);

    ARegister(in=insMuxALU, load=loadA, out[0..14]=addressM, out=ARegOut);
    DRegister(in=ALUOut, load=d2, out=DRegOut);
    
    ALU(x=DRegOut, y=AmuxM, zx=c1, nx=c2, zy=c3, ny=c4, f=c5, no=c6, out=ALUOut, out=outM, zr=ALUzr, ng=ALUng);

    And(a=ALUng, b=j1, out=JLT);
    And(a=ALUzr, b=j2, out=JEQ);
    Or(a=ALUzr, b=ALUng, out=ZrOrNg);
    Not(in=ZrOrNg, out=Pos);
    And(a=Pos, b=j3, out=JGT);

    Or(a=JEQ, b=JLT, out=JLE);
    Or(a=JLE, b=JGT, out=jump);
    PC(in=ARegOut, load=jump, inc=true, reset=reset, out[0..14]=pc);
    
}